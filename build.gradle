buildscript {
    repositories {
        maven {
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        mavenCentral()
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0.16,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
    id 'maven-publish'
}

apply plugin: 'org.spongepowered.mixin'

group = mod_group_id
version = mod_version

base {
    archivesName = mod_id
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
    withSourcesJar()
}

jarJar.enable()

minecraft {
    mappings channel: 'parchment', version: '2023.09.03-1.20.1'
    copyIdeResources = true
    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }
        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            workingDirectory project.file('run-data')
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources')
        }
    }
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"
    config "${mod_id}.mixins.json"
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

repositories {
    mavenLocal()
    maven {
        name 'Tterrag Maven'
        url 'https://maven.tterrag.com/'
    }
    maven {
        url 'https://modmaven.dev/'
    }
    maven {
        url 'https://maven.blamejared.com'
    }
    maven {
        name = "Illusive Soulworks maven"
        url = "https://maven.theillusivec4.top/"
    }
    maven {
        url "https://cursemaven.com"
    }
    maven {
        url "https://maven.firstdark.dev/snapshots"
    }
    maven {
        url = 'https://maven.minecraftforge.net/'
    }
    flatDir {
        dirs "libs"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    // Nebula Libs — 通过 mavenLocal + fg.deobf() 引入，ForgeGradle 自动将 SRG 名反映射为 Parchment 名
    // IDEA 导包和 runClient 都正常，不再需要手动切换 -dev 和正式版
    // 需要先在 Nebula-Utils 项目执行 ./gradlew publishToMavenLocal

    implementation fg.deobf("top.nebula:nebula_libs:1.5.0")
    compileOnly files("libs/Nebula Libs-1.5.0-sources.jar")
    jarJar(group: "dev.celestiacraft", name: "Nebula Libs", version: "[1.5.0,)")

    // Create + Flywheel + Registrate
    implementation fg.deobf("com.simibubi.create:create-${minecraft_version}:${create_version}:slim") { transitive = false }
    implementation fg.deobf("com.jozufozu.flywheel:flywheel-forge-${minecraft_version}:${flywheel_version}")
    implementation fg.deobf("com.tterrag.registrate:Registrate:${registrate_version}")

    // Mekanism
    implementation fg.deobf("mekanism:Mekanism:${mekanism_version}")
    // Curios
    implementation fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}")
    // TCon
    implementation fg.deobf("curse.maven:tinkers-construct-74072:6266282")
    // Mantle
    implementation fg.deobf("curse.maven:mantle-74924:7414488")
    // 汽鸣铁道
    implementation fg.deobf("curse.maven:create-steam-n-rails-688231:5840017")
    // IE
    implementation fg.deobf("curse.maven:immersive-engineering-231951:6206989")
    // II
    implementation fg.deobf("curse.maven:immersive-industry-542053:7530803")
    // Botarium
    implementation fg.deobf("curse.maven:botarium-704113:5486070")
    // Resourceful Lib
    implementation fg.deobf("curse.maven:resourceful-lib-570073:5659871")
    // Alex Cave
    implementation fg.deobf("curse.maven:alexs-caves-924854:5848216")
    // Ad Astra
    implementation fg.deobf("curse.maven:ad-astra-635042:5914893")
    // Rhino
    implementation fg.deobf("curse.maven:rhino-416294:6186971")
    // Jade
    implementation fg.deobf("curse.maven:jade-324717:6106101")
    // KubeJS
    implementation fg.deobf("curse.maven:kubejs-238086:5853326")
    // TerraBlender
    implementation fg.deobf("com.github.glitchfiend:TerraBlender-forge:1.20.1-3.0.1.10")
    jarJar(group: "com.github.glitchfiend", name: "TerraBlender-forge", version: "[1.20.1-3.0.1,1.20.1-3.0.2)")
    // ldlib
    implementation fg.deobf("com.lowdragmc.ldlib:ldlib-forge-1.20.1:1.0.45")
    jarJar(group: "com.lowdragmc.ldlib", name: "ldlib-forge-1.20.1", version: "[1.0.45,)")
    // Patchouli
    compileOnly fg.deobf("vazkii.patchouli:Patchouli:1.20.1-84.1-FORGE:api")
    runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:1.20.1-84.1-FORGE")
    // Jei
    compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}")
    compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}")
    runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")

    // architectury
    runtimeOnly fg.deobf("curse.maven:architectury-api-419699:5137938")
    // resourceful config
    runtimeOnly fg.deobf("curse.maven:resourceful-config-714059:6231176")
    // citadel
    runtimeOnly fg.deobf("curse.maven:citadel-331936:7476570")
    // Lomkok
    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'
    // Mixin
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

tasks.named('processResources', ProcessResources).configure {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    var replaceProperties = [
            minecraft_version                 : minecraft_version,
            minecraft_version_range           : minecraft_version_range,
            forge_version                     : forge_version,
            forge_version_range               : forge_version_range,
            loader_version_range              : loader_version_range,
            mod_id                            : mod_id,
            mod_name                          : mod_name,
            mod_license                       : mod_license,
            mod_version                       : mod_version,
            mod_authors                       : mod_authors,
            mod_description                   : mod_description,
            curios_version_range              : curios_version_range,
            mekanism_version_range            : mekanism_version_range,
            tconstruct_version_range          : tconstruct_version_range,
            create_version_range              : create_version_range,
            deepdrilling_version_range        : deepdrilling_version_range,
            immersiveengineering_version_range: immersiveengineering_version_range,
            jei_version_range                 : jei_version_range
    ]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes([
                "Specification-Title"     : mod_id,
                "Specification-Vendor"    : mod_authors,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
        archiveFileName.set(String.format("CMI Core-%s-%s.jar", mod_version, minecraft_version))
    }

    finalizedBy 'reobfJar'
}

tasks.named('jarJar') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes([
                "Specification-Title"     : mod_id,
                "Specification-Vendor"    : mod_authors,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
        archiveFileName.set(String.format("CMI Core-%s-%s-all.jar", mod_version, minecraft_version))
    }

    finalizedBy 'reobfJar'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-parameters"
}


// 谁删谁傻逼 ⬇️
afterEvaluate {
    minecraft.runs.data.jvmArgs.removeIf { it.contains('mixin.config') }
}
